model{
	## outcome Model
	# data level
	for(ii in 1:N){
	      Y[ii]~dnorm(yhat[ii],prec.y)
	      yhat[ii] <- alphaY+Xy[ii,]%*%gammaHat +teacherEffectY[teacher[ii]]
	      	       +tau1*tt[ii]*Z[ii]+tau2*pow(tt[ii],2)*Z[ii]+tau*Z[ii]
	      	       +lambda1*tt[ii]+lambda2*pow(tt[ii],2)
	}
	#teacher level
	for(jj in 1:nteachers){
	      teacherEffectY[jj]~dnorm(tHatY[jj],prec.tY)
	      tHatY[jj] <- schoolEffectY[school[jj]]
	}
	## school level
	for(kk in 1:nschools){
	      schoolEffectY[kk]~dnorm(pairEffect[pair[kk]],prec.sY)
	}
	## pair level
	for(ll in 1:npairs){
	       pairEffect[ll]~dnorm(0,prec.pY)
	}
	## data level
	for(i in 1:N){
	      tt[i]~dnorm(tthat[i],prec.tt)
	      tthat[i] <- Xt[i,]%*%betaHat+teacherEffectTT[teacher[i]]
	}
	## teacher level
	for(j in 1:nteachers){
	      teacherEffectTT[j]~dnorm(tHat[j],prec.t)
	      tHat[j] <- schoolEffectTT[school[j]]
	}
	## school level
	for(k in 1:nschools){
	      schoolEffectTT[k]~dnorm(alpha,prec.s)
	}
	## priors
	for(l in 1:pt){
	      betaHat[l]~dnorm(0,1/9)
	}
	for(w in 1:py){
	      gammaHat[w]~dnorm(0,1/9)
	}
	tau~dnorm(0,1/9)
	tau1~dnorm(0,1/9)
	tau2~dnorm(0,1/9)
	lambda1~dnorm(0,1/9)
	lambda2~dnorm(0,1/9)
	prec.y~dgamma(0.001,0.001)
	#sd.y <- 1/sqrt(prec.y)
	prec.tY~dgamma(0.001,0.001)
	#	sd.tY <- 1/sqrt(prec.tY)

	prec.sY~dgamma(0.001,0.001)
	#	sd.sY <- 1/sqrt(prec.sY)

	prec.pY~dgamma(0.001,0.001)
	#	sd.pY <- 1/sqrt(prec.pY)

	prec.tt~dgamma(0.001,0.001)
	#	sd.tt <- 1/sqrt(prec.tt)

	prec.t~dgamma(0.001,0.001)
	#	sd.t <- 1/sqrt(prec.t)

	prec.s~dgamma(0.001,0.001)
	#	sd.s <- 1/sqrt(prec.s)

	alphaY~dnorm(0,1/9)
	alpha~dnorm(0,1/9)


}
